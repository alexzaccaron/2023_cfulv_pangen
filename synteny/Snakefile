
# Snakemake pipeline to generate chromosomes plot

SAMPLES=["R0", "R4", "R5", "W4", "W7"]


rule all:
   input:
      expand("output/01files/{sample}_windows_genes.bed", sample=SAMPLES),
      expand("output/01files/{sample}_windows_repeats.bed", sample=SAMPLES),
      "chromosomes.pdf"


##makewindows: generate sliding window coordinates
rule makewindows:
   conda: "env.yml"
   input:
      fai="data/{sample}.fai"
   output:
      "output/01files/{sample}_windows.bed"
   shell: """
       cut -f 1,2 {input.fai}  | bedtools makewindows -w 30000 -g stdin > {output}
   """



##gene_count: count how many genes within windows
rule gene_count:
   conda: "env.yml"
   input:
      windows="output/01files/{sample}_windows.bed",
      genes="data/{sample}.bed"
   output:
      "output/01files/{sample}_windows_genes.bed"
   shell: """
       bedtools intersect -c -a {input.windows} -b {input.genes}  | awk  '{{ print $0"\t {wildcards.sample}" }}' > {output}
   """



##repeat_count: calculate the percentage of repeats within windows
rule repeat_count:
   conda: "env.yml"
   input:
      windows="output/01files/{sample}_windows.bed",
      repeats="data/{sample}_repeats.gff"
   output:
      "output/01files/{sample}_windows_repeats.bed"
   shell: """
       bedtools coverage -a {input.windows} -b {input.repeats}  | awk  '{{ print $0"\t {wildcards.sample}" }}' > {output}
   """


##plot_chromosomes: make a plot with the chromosomes
rule plot_chromosomes:
   input:
      expand("output/01files/{sample}_windows_genes.bed", sample=SAMPLES),
      expand("output/01files/{sample}_windows_repeats.bed", sample=SAMPLES),
   output:
      "chromosomes.pdf"
   shell: """
       Rscript scripts/plot_chromosomes.R
   """



